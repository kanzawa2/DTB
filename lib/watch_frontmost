#!/usr/bin/osascript

# Output frontmost window log
#
# Author: Takuya Okada
# Created: 2014-5-28

on getFrontmostProcess()
  tell application "System Events"
    return {unix id, name} of (item 1 of (processes whose frontmost is true))
  end tell
end getFrontmostProcess

on getTimeString(time)
  set {year:y, month:m, day:d, hours:hh, minutes:mm, seconds:ss} to time
  set ymd to y * 10000 + m * 100 + d
  set hms to hh * 10000 + mm * 100 + ss + 1000000 as string

  return ymd & "T" & (text 2 thru 7 of hms)
end getTimeString

on printLog(pid, pname)
  set now to getTimeString(current date) as string
  log now & "|" & pid & "|" & pname & "|"
end

################################################################
## main

set previousProcess to null

repeat
set currentProcess to getFrontmostProcess()
set {pid, pname} to currentProcess

if not (currentProcess = previousProcess) then
  printLog(pid, pname)
  set previousProcess to currentProcess
end if
delay 1 #sec
end repeat